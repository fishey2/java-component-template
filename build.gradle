plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "org.sonarqube" version "2.8"
    id "net.ossindex.audit" version "0.4.11"
}

group 'com.roboautomator'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // For Spring Boot Base
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-activemq')

    // For Swagger Implementation
    implementation('io.springfox:springfox-swagger2:2.9.2')
    implementation('io.springfox:springfox-swagger-ui:2.9.2')

    // For Logging Implementation
    implementation('ch.qos.logback:logback-classic:1.2.3')
    implementation('ch.qos.logback.contrib:logback-json-classic:0.1.5')
    implementation('ch.qos.logback.contrib:logback-jackson:0.1.5')
    implementation('org.apache.logging.log4j:log4j-to-slf4j:2.11.1')
    implementation('org.slf4j:jul-to-slf4j:1.7.25')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.junit.jupiter:junit-jupiter:5.5.2')
    testImplementation('org.mockito:mockito-core:2.24.0')
    testImplementation('io.rest-assured:rest-assured:4.1.2')
    testImplementation('io.rest-assured:json-path:4.1.2')
    testImplementation('io.rest-assured:xml-path:4.1.2')

    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
}

apply from: 'gradle/unit-test.gradle'
apply from: 'gradle/functional-test.gradle'
apply from: 'gradle/sonar.gradle'

bootJar {
    manifest {
        attributes 'Start-Class': 'com.roboautomator.component.MainApplication'
    }
    mainClassName = 'com.roboautomator.component.MainApplication'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

configurations.all {
    exclude group:'junit', module:'junit'
    exclude group:'org.junit.vintage', module:'junit-vintage-engine'
    exclude group:'org.mockito', module:'mockito-all'
    exclude group:'org.slf4j', module:'slf4j-log4j12'
    exclude group:'org.springframework.boot', module:'spring-boot-starter-logging'
}
